plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id "io.freefair.lombok" version "8.14.2"
    id "checkstyle"
    id "com.github.spotbugs" version "6.4.4"
    id "jacoco"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.qpid:qpid-jms-client:2.9.0'
    implementation 'org.springframework:spring-jms'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:8.2.1'
    implementation 'uk.nhs.connect.iucds:iucds-schema:4.0.RC1.0'
    implementation 'org.apache.xmlbeans:xmlbeans:4.0.0'
    implementation 'com.rabbitmq.jms:rabbitmq-jms:3.5.0'

    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:5.5.6'
    testImplementation 'io.rest-assured:json-path:5.5.6'
    testImplementation 'io.rest-assured:xml-path:5.5.6'
    testImplementation 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-dstu3:8.4.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test // tests are required to run before generating the report
}

task integrationTest(type: Test) {
    useJUnitPlatform() {
        description = 'Runs integration tests.'
        group = 'verification'

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }
    mustRunAfter(checkstyleMain)
    mustRunAfter(checkstyleTest)
    mustRunAfter(checkstyleIntegrationTest)
    mustRunAfter(spotbugsMain)
    mustRunAfter(spotbugsTest)
    mustRunAfter(spotbugsIntegrationTest)
}

task staticCodeAnalysis(type: GradleBuild) {
    tasks = [
            'clean',
            'checkstyleMain',
            'checkstyleTest',
            'checkstyleIntegrationTest',
            'spotbugsMain',
            'spotbugsTest',
            'spotbugsIntegrationTest',
    ]
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    excludeFilter = rootProject.file('config/spotbugs/exclude.xml')
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest
